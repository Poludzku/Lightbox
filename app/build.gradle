apply plugin: 'com.android.application'

ext {

    //RELEASE SIGNATURE
    releaseSignatureProperties = new Properties()
    releaseSignatureProperties.load(new FileInputStream(project.file('../../keystores/poludzku-lightbox-capstone.properties')))
    releaseSignatureKeystore = '../../keystores/poludzku-lightbox-capstone.keystore'

    //DEBUG SIGNATURE
    debugSignatureKeystore = '../../keystores/poludzku-lightbox-capstone-debug.keystore'
    //LIBRARY VERSIONS
    autoValueVersion = '1.4.1'
    rxJavaVersion = '2.0.9'
    rxAndroidVersion = '2.0.1'
    butterKnifeVersion = '8.5.1'
    daggerVersion = '2.10'
    jrs250Version = '1.0'
    appCompatVersion = '27.0.2'
    architectureComponentsVersion = '1.0.0'
    playServicesVersion = '11.6.2'

    testingSupportVersion = '0.5'
    mockitoVersion = '2.7.22'
    assertjVersion = '3.5.2'
    assertJAndroidVersion = '1.1.1'
    junitVersion = '4.12'
    espressoVersion = '2.2.2'
    testingSupportVersion = '0.5'
    timberVersion = '4.6.0'

}

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.2"

    defaultConfig {
        applicationId "com.poludzku.lightbox"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        releaseConfig {
            storeFile file(releaseSignatureKeystore)
            storePassword releaseSignatureProperties.keystore_password
            keyAlias releaseSignatureProperties.key_alias
            keyPassword releaseSignatureProperties.key_password
        }
        debugConfig {
            storeFile file(debugSignatureKeystore)
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            useProguard false
            debuggable true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debugConfig
        }
        release {
            minifyEnabled false
            useProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.releaseConfig
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "com.jakewharton.timber:timber:$timberVersion"

    implementation "com.android.support:appcompat-v7:$appCompatVersion"
    implementation "com.android.support:design:$appCompatVersion"
    implementation "com.android.support:support-annotations:$appCompatVersion"

    implementation "com.google.android.gms:play-services-drive:$playServicesVersion"
    implementation "com.google.android.gms:play-services-auth:$playServicesVersion"

    implementation "android.arch.lifecycle:runtime:$architectureComponentsVersion"
    implementation "android.arch.lifecycle:extensions:$architectureComponentsVersion"
    annotationProcessor "android.arch.lifecycle:compiler:$architectureComponentsVersion"

    //INJECTION
    implementation "javax.annotation:jsr250-api:$jrs250Version"
    implementation "com.google.dagger:dagger:$daggerVersion"
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    annotationProcessor "com.google.dagger:dagger-compiler:$daggerVersion"

    //VIEW INJECTION
    implementation "com.jakewharton:butterknife:$butterKnifeVersion"
    annotationProcessor "com.jakewharton:butterknife-compiler:$butterKnifeVersion"

    //AUTOVALUE
    provided "com.google.auto.value:auto-value:$autoValueVersion"
    annotationProcessor "com.google.auto.value:auto-value:$autoValueVersion"

    //RX
    implementation "io.reactivex.rxjava2:rxjava:$rxJavaVersion"
    implementation "io.reactivex.rxjava2:rxandroid:$rxAndroidVersion"

    //UNIT TESTING
    testCompile "junit:junit:$junitVersion"
    testCompile "org.mockito:mockito-core:$mockitoVersion"
    testCompile "org.mockito:mockito-android:$mockitoVersion"
    testCompile "org.assertj:assertj-core:$assertjVersion"
    testCompile("com.squareup.assertj:assertj-android:$assertJAndroidVersion") {
        exclude group: 'com.android.support', module: 'support-annotations'
    }

    // ESPRESSO INSTRUMENTATION TESTING
    //TODO remove excludes once library is updated
    androidTestCompile("com.android.support.test.espresso:espresso-core:$espressoVersion") {
        exclude module: 'support-annotations'
        exclude module: 'appcompat-v7'
        exclude module: 'design'
        exclude module: 'recyclerview-v7'
        exclude module: 'support-v4'
    }
    androidTestCompile("com.android.support.test.espresso:espresso-contrib:$espressoVersion") {
        exclude module: 'support-annotations'
        exclude module: 'appcompat-v7'
        exclude module: 'design'
        exclude module: 'recyclerview-v7'
        exclude module: 'support-v4'
    }
    androidTestCompile("com.android.support.test.espresso:espresso-intents:$espressoVersion") {
        exclude module: 'support-annotations'
    }
    androidTestCompile("com.android.support.test:runner:$testingSupportVersion") {
        exclude module: 'support-annotations'
    }
    androidTestCompile("com.android.support.test:rules:$testingSupportVersion") {
        exclude module: 'support-annotations'
    }
    androidTestCompile("com.squareup.assertj:assertj-android:$assertJAndroidVersion") {
        exclude module: 'support-annotations'
    }

    implementation 'com.android.support:cardview-v7:27.0.2'
}
